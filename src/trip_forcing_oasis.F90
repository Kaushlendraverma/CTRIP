SUBROUTINE TRIP_FORCING_OASIS(TP, TPG, TPLK, TPST,                  &
                              KLISTING, KSTATE, KLON, KLAT, PTIMEC, &
                              PDRAIN, PRUNOFF, PSRC_FLOOD, PCALVING )
!#############################################
!
!!****  *TRIP_FORCING_OASIS*
!!
!!    PURPOSE
!!    -------
!!
!!    Get forcing from OASIS coupler (online mode)
!!
!!    REFERENCE
!!    ---------
!!
!!    AUTHOR
!!    ------
!!      S. Munier
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    04/2020
!-------------------------------------------------------------------------------
!
!*       0.     DECLARATIONS
!               ------------
!
USE MODD_TRIP,       ONLY : TRIP_t
USE MODD_TRIP_GRID,  ONLY : TRIP_GRID_t
USE MODD_TRIP_LAKE,  ONLY : TRIP_LAKE_t
USE MODD_TRIP_STATE, ONLY : TRIP_STATE_t
!
USE MODD_TRIP_LISTING
USE MODD_TRIP_MPI
USE MODD_TRIP_OASIS
!
USE MODN_TRIP    , ONLY : CLAKE
!
USE MODE_TRIP_GRID_STATE
USE MODE_TRIP_LAKE
!
USE MODI_TRIP_OASIS_RECV
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
#ifdef SFX_MPI
INCLUDE "mpif.h"
#endif
!
!-------------------------------------------------------------------------------
!
!*      0.1    declarations of arguments
!
!
TYPE(TRIP_t),       INTENT(INOUT) :: TP
TYPE(TRIP_GRID_t),  INTENT(INOUT) :: TPG
TYPE(TRIP_LAKE_t),  INTENT(INOUT) :: TPLK
TYPE(TRIP_STATE_t), INTENT(INOUT) :: TPST
!
INTEGER, INTENT(IN)  :: KLISTING      ! Listing ID
INTEGER, INTENT(IN)  :: KSTATE        ! Size of state vector
INTEGER, INTENT(IN)  :: KLON          ! Number of longitude if forcing offline
INTEGER, INTENT(IN)  :: KLAT          ! Number of latittude if forcing offline
REAL,    INTENT(IN)  :: PTIMEC        ! Cumulated time
!
REAL, DIMENSION(KSTATE), INTENT(OUT) :: PRUNOFF           ! Surface runoff               (kg/s)
REAL, DIMENSION(KSTATE), INTENT(OUT) :: PDRAIN            ! Drainage                     (kg/s)
REAL, DIMENSION(KSTATE), INTENT(OUT) :: PCALVING          ! Calving flux                 (kg/s)
REAL, DIMENSION(KSTATE), INTENT(OUT) :: PSRC_FLOOD        ! Input P-E-I flood source term(kg/s)
!
!-------------------------------------------------------------------------------
!
!*      0.2    declarations of local variables
!
REAL, DIMENSION(:,:),   ALLOCATABLE :: ZDRAIN_OASIS_READ     ! Drainage from OASIS
REAL, DIMENSION(:,:),   ALLOCATABLE :: ZRUNOFF_OASIS_READ    ! Surface runoff from OASIS
REAL, DIMENSION(:,:),   ALLOCATABLE :: ZSRC_FLOOD_OASIS_READ ! Flood source term from OASIS
REAL, DIMENSION(:,:),   ALLOCATABLE :: ZCALVING_OASIS_READ   ! Calving flux from OASIS
REAL, DIMENSION(:),     ALLOCATABLE :: ZVECTOR
!
INTEGER :: IERR
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
! --------------------------------------------------------------------------------------
!
IF (LHOOK) CALL DR_HOOK('TRIP_FORCING_OASIS',0,ZHOOK_HANDLE)
!
IF(NRANK==NPIO)THEN
  ALLOCATE(ZDRAIN_OASIS_READ(KLON,KLAT))
  ALLOCATE(ZRUNOFF_OASIS_READ(KLON,KLAT))
  ALLOCATE(ZSRC_FLOOD_OASIS_READ(KLON,KLAT))
  ALLOCATE(ZCALVING_OASIS_READ(KLON,KLAT))
  ALLOCATE(ZVECTOR(TPST%NNSTATE))
ELSE
  ALLOCATE(ZDRAIN_OASIS_READ(0,0))
  ALLOCATE(ZRUNOFF_OASIS_READ(0,0))
  ALLOCATE(ZSRC_FLOOD_OASIS_READ(0,0))
  ALLOCATE(ZCALVING_OASIS_READ(0,0))
  ALLOCATE(ZVECTOR(0))
ENDIF
!
!   Receive forcings from OASIS
!
CALL MPI_GATHER(TPST%XSURF_STO,KSTATE,MPI_DOUBLE,ZVECTOR,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
IF(NRANK==NPIO) CALL TRIP_STATE_TO_GRID(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZVECTOR,TP%XSURF_STO)
!
IF(LCPL_FLOOD)THEN
  CALL MPI_GATHER(TPST%XFLOOD_STO,KSTATE,MPI_DOUBLE,ZVECTOR,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
  IF(NRANK==NPIO) CALL TRIP_STATE_TO_GRID(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZVECTOR,TP%XFLOOD_STO)
ENDIF
!
IF(NRANK==NPIO)THEN
  CALL TRIP_OASIS_RECV(TP, TPG, &
                       KLISTING,KLON,KLAT,PTIMEC,ZRUNOFF_OASIS_READ,  &
                       ZDRAIN_OASIS_READ,ZCALVING_OASIS_READ,ZSRC_FLOOD_OASIS_READ)
ENDIF
!
IF(NRANK==NPIO)THEN
  IF (CLAKE=='MLK') THEN
    CALL TRIP_GRID_TO_LAKE(TPST%NSTATE_IND,TPLK%NLAKE_ID_IN,TPST%NLAKE_STATE, &
                            TPLK%XFRAC_LAKE,ZRUNOFF_OASIS_READ,ZVECTOR        )
  ELSE
    CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZRUNOFF_OASIS_READ,ZVECTOR)
  ENDIF
ENDIF
CALL MPI_SCATTER(ZVECTOR,KSTATE,MPI_DOUBLE,PRUNOFF,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
IF(NRANK==NPIO)THEN
  IF (CLAKE=='MLK') THEN
    CALL TRIP_GRID_TO_LAKE(TPST%NSTATE_IND,TPLK%NLAKE_ID_IN,TPST%NLAKE_STATE, &
                            TPLK%XFRAC_LAKE,ZDRAIN_OASIS_READ,ZVECTOR        )
  ELSE
    CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZDRAIN_OASIS_READ,ZVECTOR)
  ENDIF
ENDIF
CALL MPI_SCATTER(ZVECTOR,KSTATE,MPI_DOUBLE,PDRAIN,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
IF(LCPL_FLOOD)THEN
  IF(NRANK==NPIO)THEN
    IF (CLAKE=='MLK') THEN
      CALL TRIP_GRID_TO_LAKE(TPST%NSTATE_IND,TPLK%NLAKE_ID_IN,TPST%NLAKE_STATE, &
                              TPLK%XFRAC_LAKE,ZSRC_FLOOD_OASIS_READ,ZVECTOR        )
    ELSE
      CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZSRC_FLOOD_OASIS_READ,ZVECTOR)
    ENDIF
  ENDIF
  CALL MPI_SCATTER(ZVECTOR,KSTATE,MPI_DOUBLE,PSRC_FLOOD,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
ENDIF
!
IF(NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,ZCALVING_OASIS_READ,ZVECTOR)
CALL MPI_SCATTER(ZVECTOR,KSTATE,MPI_DOUBLE,PCALVING,KSTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
! --------------------------------------------------------------------------------------
!
DEALLOCATE(ZDRAIN_OASIS_READ)
DEALLOCATE(ZRUNOFF_OASIS_READ)
DEALLOCATE(ZSRC_FLOOD_OASIS_READ)
DEALLOCATE(ZCALVING_OASIS_READ)
DEALLOCATE(ZVECTOR)
!
IF (LHOOK) CALL DR_HOOK('TRIP_FORCING_OASIS',1,ZHOOK_HANDLE)
!
END SUBROUTINE TRIP_FORCING_OASIS
