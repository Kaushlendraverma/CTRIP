!####################################################################################
SUBROUTINE INIT_TRIP_STATE (TP, TPG, TPLK, TPST, TPDG)
!####################################################################################
!
!****  *INIT_TRIP_STATE*
!
!    PURPOSE
!    -------
!
!     Initialize TRIP state vectors
!
!**  METHOD
!    ------
!
!     Direct calculation
!
!    EXTERNAL
!    --------
!
!     None
!
!    IMPLICIT ARGUMENTS
!    ------------------
!
!
!    REFERENCE
!    ---------
!
!    AUTHOR
!    ------
!      S. Munier
!
!    MODIFICATIONS
!    -------------
!      Original    07/2019
!-------------------------------------------------------------------------------
!
!*       0.     DECLARATIONS
!               ------------
!
USE MODD_TRIP,       ONLY : TRIP_t
USE MODD_TRIP_GRID,  ONLY : TRIP_GRID_t
USE MODD_TRIP_LAKE,  ONLY : TRIP_LAKE_t
USE MODD_TRIP_STATE, ONLY : TRIP_STATE_t
USE MODD_TRIP_DIAG,  ONLY : TRIP_DIAG_t
!
USE MODN_TRIP, ONLY : CGROUNDW, CVIT, LFLOOD, CLAKE, XBANKSLOPE, LBACKWATER
!
USE MODD_TRIP_PAR, ONLY : NDIMTAB
USE MODD_TRIP_MPI
!
USE MODE_TRIP_GRID_STATE, ONLY : TRIP_GRID_TO_STATE
!
USE MODI_ALLOC_TRIP_DIAG_STATE
USE MODI_WAITING_MPI
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
#ifdef SFX_MPI
INCLUDE "mpif.h"
#endif
!
!-------------------------------------------------------------------------------
!
!*      0.1    declarations of arguments
!
TYPE(TRIP_t),       INTENT(INOUT) :: TP
TYPE(TRIP_GRID_t),  INTENT(INOUT) :: TPG
TYPE(TRIP_LAKE_t),  INTENT(INOUT) :: TPLK
TYPE(TRIP_STATE_t), INTENT(INOUT) :: TPST
TYPE(TRIP_DIAG_t),  INTENT(INOUT) :: TPDG
!
!-------------------------------------------------------------------------------
!
!*      0.2    declarations of local variables
!
INTEGER, ALLOCATABLE, DIMENSION(:)   :: IVECTOR
REAL,    ALLOCATABLE, DIMENSION(:)   :: ZVECTOR
REAL,    ALLOCATABLE, DIMENSION(:,:) :: ZVECTOR2
REAL,    ALLOCATABLE, DIMENSION(:,:) :: ZVECTOR3
LOGICAL, ALLOCATABLE, DIMENSION(:)   :: GVECTOR
INTEGER, ALLOCATABLE, DIMENSION(:)   :: IPREVN
INTEGER, ALLOCATABLE, DIMENSION(:,:) :: IPREVST
!
INTEGER :: JLON, JLAT, JSTATE, ISTATE
!
INTEGER :: IERR
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('INIT_TRIP_STATE',0,ZHOOK_HANDLE)
!
CALL WAITING_MPI('INIT_TRIP_STATE')
!
ISTATE = TPST%NNSTATE_P
!
!-------------------------------------------------------------------------------
! * Allocate trip state arguments
!-------------------------------------------------------------------------------
!
ALLOCATE(TPST%XAREA     (ISTATE))
ALLOCATE(TPST%XLEN      (ISTATE))
ALLOCATE(TPST%NGRCN     (ISTATE))
ALLOCATE(TPST%NSEQ      (ISTATE))
ALLOCATE(TPST%NNEXTST   (ISTATE))
ALLOCATE(TPST%NPREVST   (8,ISTATE))
ALLOCATE(TPST%NBASID    (ISTATE))
!
ALLOCATE(TPST%GMASK     (ISTATE))
ALLOCATE(TPST%GMASK_VEL (ISTATE))
ALLOCATE(TPST%GMASK_FLD (ISTATE))
ALLOCATE(TPST%GMASK_LAKE(ISTATE))
ALLOCATE(TPST%GMASK_GW  (ISTATE))
ALLOCATE(TPST%GMASK_GRE (ISTATE))
ALLOCATE(TPST%GMASK_ANT (ISTATE))
!
ALLOCATE(TPST%XSURF_STO (ISTATE))
!
IF(CGROUNDW/='DEF')THEN
  ALLOCATE(TPST%XTAUG(ISTATE))
ELSE
  ALLOCATE(TPST%XTAUG(0))
ENDIF
!
IF(CGROUNDW=='CST')THEN
  ALLOCATE(TPST%XGROUND_STO(ISTATE))
ELSE
  ALLOCATE(TPST%XGROUND_STO(0))
ENDIF
!
IF(CGROUNDW=='DIF')THEN
  ALLOCATE(TPST%XHGROUND (ISTATE))
  ALLOCATE(TPST%XWEFF    (ISTATE))
  ALLOCATE(TPST%XTRANS   (ISTATE))
  ALLOCATE(TPST%XNUM_AQUI(ISTATE))
  ALLOCATE(TPST%XTABGW_F (ISTATE,NDIMTAB))
  ALLOCATE(TPST%XTABGW_H (ISTATE,NDIMTAB))
ELSE
  ALLOCATE(TPST%XHGROUND (0))
  ALLOCATE(TPST%XWEFF    (0))
  ALLOCATE(TPST%XTRANS   (0))
  ALLOCATE(TPST%XNUM_AQUI(0))
  ALLOCATE(TPST%XTABGW_F (0,0))
  ALLOCATE(TPST%XTABGW_H (0,0))
ENDIF
!
IF(CGROUNDW=='DIF'.OR.LBACKWATER)THEN
  ALLOCATE(TPST%XTOPO_RIV(ISTATE))
ELSE
  ALLOCATE(TPST%XTOPO_RIV(0))
ENDIF
!
IF(LFLOOD.OR.CGROUNDW=='DIF'.OR.XBANKSLOPE>0.0)THEN
  ALLOCATE(TPST%XHC_BED(ISTATE))
ELSE
  ALLOCATE(TPST%XHC_BED(0))
ENDIF
!
IF(LFLOOD)THEN
  ALLOCATE(TPST%XN_FLOOD  (ISTATE))
  ALLOCATE(TPST%XFLOOD_STO(ISTATE))
  ALLOCATE(TPST%XWFLOOD   (ISTATE))
  ALLOCATE(TPST%XFLOOD_LEN(ISTATE))
  ALLOCATE(TPST%XHFLOOD   (ISTATE))
  ALLOCATE(TPST%XFFLOOD   (ISTATE))
  ALLOCATE(TPST%XTAB_F    (ISTATE,NDIMTAB+1))
  ALLOCATE(TPST%XTAB_H    (ISTATE,NDIMTAB+1))
  ALLOCATE(TPST%XTAB_VF   (ISTATE,NDIMTAB+1))
ELSE
  ALLOCATE(TPST%XN_FLOOD  (0))
  ALLOCATE(TPST%XFLOOD_STO(0))
  ALLOCATE(TPST%XWFLOOD   (0))
  ALLOCATE(TPST%XFLOOD_LEN(0))
  ALLOCATE(TPST%XHFLOOD   (0))
  ALLOCATE(TPST%XFFLOOD   (0))
  ALLOCATE(TPST%XTAB_F    (0,0))
  ALLOCATE(TPST%XTAB_H    (0,0))
  ALLOCATE(TPST%XTAB_VF   (0,0))
ENDIF
!
IF(CVIT=='VAR')THEN
  ALLOCATE(TPST%XSLOPEBED(ISTATE))
  ALLOCATE(TPST%XWIDTH   (ISTATE))
  ALLOCATE(TPST%XN       (ISTATE))
ELSE
  ALLOCATE(TPST%XSLOPEBED(0))
  ALLOCATE(TPST%XWIDTH   (0))
  ALLOCATE(TPST%XN       (0))
ENDIF
!
IF(CLAKE=='MLK')THEN
  ALLOCATE(TPST%XWEIR_Z(ISTATE))
  ALLOCATE(TPST%XWEIR_W(ISTATE))
ELSE
  ALLOCATE(TPST%XWEIR_Z(0))
  ALLOCATE(TPST%XWEIR_W(0))
ENDIF
!
IF (NRANK==NPIO) THEN
  ALLOCATE(IVECTOR(TPST%NNSTATE))
  ALLOCATE(ZVECTOR(TPST%NNSTATE))
  ALLOCATE(GVECTOR(TPST%NNSTATE))
ELSE
  ALLOCATE(IVECTOR(0))
  ALLOCATE(ZVECTOR(0))
  ALLOCATE(GVECTOR(0))
ENDIF
!
!-------------------------------------------------------------------------------
! * Compute and Read TRIP parameter
!-------------------------------------------------------------------------------
!
! * Flow direction
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%NGRCN,IVECTOR)
CALL MPI_SCATTER(IVECTOR,ISTATE,MPI_INTEGER,TPST%NGRCN,ISTATE,MPI_INTEGER,NPIO,NCOMM,IERR)
!
! * Land mask
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK,GVECTOR)
CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
! * Rriver sequence
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%NSEQ,IVECTOR)
CALL MPI_SCATTER(IVECTOR,ISTATE,MPI_INTEGER,TPST%NSEQ,ISTATE,MPI_INTEGER,NPIO,NCOMM,IERR)
!
TPST%NSEQMAX = TPG%NSEQMAX
!
! * Basin number id
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%NBASID,IVECTOR)
CALL MPI_SCATTER(IVECTOR,ISTATE,MPI_INTEGER,TPST%NBASID,ISTATE,MPI_INTEGER,NPIO,NCOMM,IERR)
!
IF (NRANK==NPIO) TPST%NBASMIN = TPG%NBASMIN
CALL MPI_BCAST(TPST%NBASMIN,1,MPI_INTEGER,NPIO,NCOMM,IERR)
IF (NRANK==NPIO) TPST%NBASMAX = TPG%NBASMAX
CALL MPI_BCAST(TPST%NBASMAX,1,MPI_INTEGER,NPIO,NCOMM,IERR)
!
! * Set down stream
!
IF (NRANK==NPIO) THEN
  ALLOCATE(IPREVN(TPST%NNSTATE))
  ALLOCATE(IPREVST(8,TPST%NNSTATE))
  IVECTOR(:) = -1
  IPREVN(:) = 0
  IPREVST(:,:) = -1
  DO JSTATE = 1,TPST%NNSTATE
    JLON = TPST%NSTATE_LON(JSTATE)
    JLAT = TPST%NSTATE_LAT(JSTATE)
    IF (JLON==-1) CYCLE
    IF (TPG%NGRCN(JLON,JLAT)>=1.AND.TPG%NGRCN(JLON,JLAT)<=8) THEN
      IVECTOR(JSTATE) = TPST%NSTATE_IND_P(TPG%NNEXTX(JLON,JLAT),TPG%NNEXTY(JLON,JLAT))
      IPREVN(IVECTOR(JSTATE)) = IPREVN(IVECTOR(JSTATE))+1
      IPREVST(IPREVN(IVECTOR(JSTATE)),IVECTOR(JSTATE)) = JSTATE
    ENDIF
  ENDDO
  DEALLOCATE(IPREVN)
ENDIF
CALL MPI_SCATTER(IVECTOR,ISTATE,MPI_INTEGER,TPST%NNEXTST,ISTATE,MPI_INTEGER,NPIO,NCOMM,IERR)
CALL MPI_SCATTER(IPREVST,8*ISTATE,MPI_INTEGER,TPST%NPREVST,8*ISTATE,MPI_INTEGER,NPIO,NCOMM,IERR)
IF (NRANK==NPIO) DEALLOCATE(IPREVST)
!
! * Set area size
!
IF (NRANK==NPIO) THEN
  CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%XAREA,ZVECTOR)
  IF (CLAKE=='MLK') THEN
    DO JSTATE = 1,TPST%NNSTATE
      JLON = TPST%NSTATE_LON(JSTATE)
      JLAT = TPST%NSTATE_LAT(JSTATE)
      IF (JLON==-1) CYCLE
      IF (TPG%GMASK_LAKE_NW(JLON,JLAT)) THEN
        ZVECTOR(JSTATE) = TPLK%XLAKE_A(TPLK%NLAKE_ID_NW(JLON,JLAT))
      ENDIF
    ENDDO
  ENDIF
ENDIF
CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XAREA,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
! * Distance between grids with the meandering ratio
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%XLEN,ZVECTOR)
CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XLEN,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
! * Land mask for Greenland and Antarctica
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_GRE,GVECTOR)
CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_GRE,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_ANT,GVECTOR)
CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_ANT,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
! * Variable velocity schemes variables
!
IF(CVIT == 'VAR')THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XN,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XN,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XWIDTH,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XWIDTH,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XSLOPEBED,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XSLOPEBED,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_VEL,GVECTOR)
  CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_VEL,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
ENDIF
!
! * Groundwater parameters
!
IF(CGROUNDW/='DEF')THEN
!
! * Groundwater transfert time
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTAUG,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XTAUG,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_GW,GVECTOR)
  CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_GW,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
ENDIF
!
IF(CGROUNDW == 'DIF')THEN
!
! * Diffusive groundwater scheme variables (only those required for state variables)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XWEFF,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XWEFF,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) THEN
    ALLOCATE(ZVECTOR2(NDIMTAB,TPST%NNSTATE))
  ELSE
    ALLOCATE(ZVECTOR2(0,0))
  ENDIF
  ALLOCATE(ZVECTOR3(NDIMTAB,ISTATE))
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTABGW_F,ZVECTOR2)
  CALL MPI_SCATTER(ZVECTOR2,ISTATE*NDIMTAB,MPI_DOUBLE,ZVECTOR3,ISTATE*NDIMTAB,MPI_DOUBLE,NPIO,NCOMM,IERR)
  TPST%XTABGW_F = TRANSPOSE(ZVECTOR3)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTABGW_H,ZVECTOR2)
  CALL MPI_SCATTER(ZVECTOR2,ISTATE*NDIMTAB,MPI_DOUBLE,ZVECTOR3,ISTATE*NDIMTAB,MPI_DOUBLE,NPIO,NCOMM,IERR)
  TPST%XTABGW_H = TRANSPOSE(ZVECTOR3)
!
  DEALLOCATE(ZVECTOR2)
  DEALLOCATE(ZVECTOR3)
!
ENDIF
!
IF(CGROUNDW=='DIF'.OR.LBACKWATER)THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTOPO_RIV,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XTOPO_RIV,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ENDIF
!
IF (LFLOOD.OR.CGROUNDW == 'DIF'.OR.XBANKSLOPE>0.0) THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XHC_BED,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XHC_BED,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ENDIF
!
! * Floodplains parameters
!
IF(LFLOOD)THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XN_FLOOD,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XN_FLOOD,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_FLD,GVECTOR)
  CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_FLD,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) THEN
    ALLOCATE(ZVECTOR2(NDIMTAB+1,TPST%NNSTATE))
  ELSE
    ALLOCATE(ZVECTOR2(0,0))
  ENDIF
  ALLOCATE(ZVECTOR3(NDIMTAB+1,ISTATE))
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTAB_F,ZVECTOR2)
  CALL MPI_SCATTER(ZVECTOR2,ISTATE*(NDIMTAB+1),MPI_DOUBLE,ZVECTOR3,ISTATE*(NDIMTAB+1),MPI_DOUBLE,NPIO,NCOMM,IERR)
  TPST%XTAB_F = TRANSPOSE(ZVECTOR3)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTAB_H,ZVECTOR2)
  CALL MPI_SCATTER(ZVECTOR2,ISTATE*(NDIMTAB+1),MPI_DOUBLE,ZVECTOR3,ISTATE*(NDIMTAB+1),MPI_DOUBLE,NPIO,NCOMM,IERR)
  TPST%XTAB_H = TRANSPOSE(ZVECTOR3)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XTAB_VF,ZVECTOR2)
  CALL MPI_SCATTER(ZVECTOR2,ISTATE*(NDIMTAB+1),MPI_DOUBLE,ZVECTOR3,ISTATE*(NDIMTAB+1),MPI_DOUBLE,NPIO,NCOMM,IERR)
  TPST%XTAB_VF = TRANSPOSE(ZVECTOR3)
!
  DEALLOCATE(ZVECTOR2)
  DEALLOCATE(ZVECTOR3)
!
ENDIF
!
IF(CLAKE=='MLK')THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TPG%GMASK_LAKE_NW,GVECTOR)
  CALL MPI_SCATTER(GVECTOR,ISTATE,MPI_LOGICAL,TPST%GMASK_LAKE,ISTATE,MPI_LOGICAL,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XWEIR_Z,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XWEIR_Z,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XWEIR_W,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XWEIR_W,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ENDIF
!
!-------------------------------------------------------------------------------
! * Read initial and historical variables
!-------------------------------------------------------------------------------
!
IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XSURF_STO,ZVECTOR)
CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XSURF_STO,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
IF(CGROUNDW=='CST')THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XGROUND_STO,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XGROUND_STO,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ELSEIF(CGROUNDW=='DIF')THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XHGROUND,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XHGROUND,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ENDIF
!
IF(LFLOOD)THEN
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XFLOOD_STO,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XFLOOD_STO,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XFFLOOD,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XFFLOOD,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XHFLOOD,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XHFLOOD,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XFLOOD_LEN,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XFLOOD_LEN,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
  IF (NRANK==NPIO) CALL TRIP_GRID_TO_STATE(TPST%NSTATE_LON,TPST%NSTATE_LAT,TP%XWFLOOD,ZVECTOR)
  CALL MPI_SCATTER(ZVECTOR,ISTATE,MPI_DOUBLE,TPST%XWFLOOD,ISTATE,MPI_DOUBLE,NPIO,NCOMM,IERR)
!
ENDIF
!
!-------------------------------------------------------------------------------
!
DEALLOCATE(IVECTOR)
DEALLOCATE(ZVECTOR)
DEALLOCATE(GVECTOR)
!
!-------------------------------------------------------------------------------
! * Alloc diag variables
!-------------------------------------------------------------------------------
!
CALL ALLOC_TRIP_DIAG_STATE(TPDG,ISTATE)
!
!-------------------------------------------------------------------------------
!
IF (LHOOK) CALL DR_HOOK('INIT_TRIP_STATE',1,ZHOOK_HANDLE)
!
!-------------------------------------------------------------------------------
! * END
!-------------------------------------------------------------------------------
END SUBROUTINE INIT_TRIP_STATE





