!TRP_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!TRP_LIC This is part of the CTRIP software governed by the CeCILL-C licence
!TRP_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!TRP_LIC for details. version 1.
!     ######
SUBROUTINE READ_FORCING_DATE(KLISTING,HFILE,KNB_TSTEP_RUN, &
                             KYEAR,KMONTH,KDAY,PTIME       )
!#############################################################
!
USE MODD_TRIP_PAR, ONLY : LNCPRINT
!
USE MODN_TRIP_RUN, ONLY : LISBAFRC, XTSTEP_RUN
!
USE MODE_TRIP_NETCDF
USE MODE_TRIP_DATE_NETCDF
!
USE MODI_ABORT_TRIP
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
USE NETCDF
!
IMPLICIT NONE
!
!*      0.1   Declarations of arguments
!
INTEGER,          INTENT(IN)       :: KLISTING
CHARACTER(LEN=*), INTENT(IN)       :: HFILE
INTEGER,          INTENT(IN)       :: KNB_TSTEP_RUN
!
INTEGER,          INTENT(OUT)      :: KYEAR
INTEGER,          INTENT(OUT)      :: KMONTH
INTEGER,          INTENT(OUT)      :: KDAY
REAL,             INTENT(OUT)      :: PTIME
!
!*      0.2   Declarations of local variables
!
CHARACTER(LEN=NF90_MAX_NAME) :: YFNAME
CHARACTER(LEN=NF90_MAX_NAME) :: YVAR
CHARACTER(LEN=NF90_MAX_NAME) :: YUNITS
!
REAL, DIMENSION(KNB_TSTEP_RUN) :: ZTIME
!
INTEGER :: IFILE_ID
INTEGER :: IVAR_ID
INTEGER :: IRET
INTEGER :: J
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('READ_FORCING_DATE',0,ZHOOK_HANDLE)
!
YFNAME = HFILE(1:LEN_TRIM(HFILE))   
YVAR   = 'time'
!
IRET=NF90_OPEN(YFNAME,NF90_NOWRITE,IFILE_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'READ_FORCING_DATE : error in NF90_OPEN'
  CALL ABORT_TRIP('READ_FORCING_DATE : error in NF90_OPEN')
ENDIF
!
IRET=NF90_INQ_VARID(IFILE_ID,YVAR,IVAR_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'READ_FORCING_DATE : error in NF90_INQ_VARID'
  CALL ABORT_TRIP('READ_FORCING_DATE : error in NF90_INQ_VARID')
ENDIF
!
IRET=NF90_GET_ATT(IFILE_ID,IVAR_ID,'units',YUNITS)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'READ_FORCING_DATE : error in NF90_GET_ATT'
  CALL ABORT_TRIP('READ_FORCING_DATE : error in NF90_GET_ATT')
ENDIF
!
CALL NCREAD(KLISTING,IFILE_ID,YVAR,ZTIME,LNCPRINT)
!
IRET=NF90_CLOSE(IFILE_ID)
IF(IRET/=NF90_NOERR)THEN
  WRITE(KLISTING,*)'READ_FORCING_DATE : error in NF90_CLOSE'
  CALL ABORT_TRIP('READ_FORCING_DATE : error in NF90_CLOSE')
ENDIF
!
CALL READ_DATE(ZTIME,YUNITS,KYEAR,KMONTH,KDAY,PTIME)
!
IF (LISBAFRC) PTIME = PTIME-XTSTEP_RUN
!
IF (LHOOK) CALL DR_HOOK('READ_FORCING_DATE',1,ZHOOK_HANDLE)
!-------------------------------------------------------------------------------
END SUBROUTINE READ_FORCING_DATE
